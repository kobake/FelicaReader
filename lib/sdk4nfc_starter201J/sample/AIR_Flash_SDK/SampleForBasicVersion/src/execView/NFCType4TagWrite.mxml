<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" label="NFCType4TagWrite" width="545" height="354" backgroundColor="#FFFFFF">
	<!-- 設定用テキストインプット -->
	<mx:Button x="10" y="10" label="実行" id="btnExecute" click="onExecute()" width="85"/>
	
	<mx:HRule x="10" y="156" width="100%"/>
	
	<!-- 実行結果表示用 -->
	<mx:Label x="10" y="168" text="IDm" fontSize="10" enabled="false"/>
	<mx:TextInput x="58" y="166" fontSize="10" width="152" id="txtIDm" editable="false" enabled="false"/>
	<mx:Label x="218" y="168" text="PMm" enabled="false"/>
	<mx:TextInput x="255" y="167" fontSize="10" width="152" id="txtPMm" editable="false" enabled="false"/>
	
	<mx:VBox x="10" y="37" height="111" width="525">
		<mx:HBox width="100%">
			<mx:Label text="Data"/>
			<mx:CheckBox label="ASCII表記" id="chkDataAscii" click="onChangeData()" selected="true"/>
		</mx:HBox>
		<mx:TextArea width="100%" height="82" id="txtDataArea" editable="true" text="Demo Application ...."/>
	</mx:VBox>
	
	<mx:HBox x="10" y="197" width="525">
		<mx:Label text="ステータスフラグ１" enabled="false"/>
		<mx:TextArea height="19" width="52" textAlign="right" id="txtStatusFlag1" editable="false" wordWrap="false" enabled="false"/>
		<mx:Label text="ステータスフラグ２" enabled="false"/>
		<mx:TextArea width="52" height="19" textAlign="right" id="txtStatusFlag2" wordWrap="false" editable="false" enabled="false"/>
	</mx:HBox>
	<mx:HBox x="10" y="223" width="525">
		<mx:Label text="felica error code" width="96"/>
		<mx:TextArea height="19" width="36" textAlign="right" id="txtFeliCaErrorCode" editable="false" wordWrap="false"/>
		<mx:Label text="rw error code"/>
		<mx:TextArea width="36" height="19" textAlign="right" id="txtRWErrorCode" wordWrap="false" editable="false"/>
		<mx:Label text="NFC Proxy error code" width="133"/>
		<mx:TextArea width="36" height="19" textAlign="right" id="txtNFCProxyErrorCode" wordWrap="false" editable="false"/>
	</mx:HBox>
	
	<!-- ログ表示用 -->
	<mx:VBox x="10" y="250" height="94" width="525">
		<mx:Label text="Log"/>
		<mx:TextArea width="100%" height="58" id="txtLogArea" editable="false" />
	</mx:VBox>
	
	<mx:Script>
		<![CDATA[
			import com.sony.jp.felica.NFCType4TagRequest;
			import com.sony.jp.felica.NFCType4TagResponse;
			import com.sony.jp.felica.event.FeliCaStatusEvent;
			import com.sony.jp.felica.error.FeliCaAccessError;
			
			import util.Util;
			
			//	チェックボックスがクリックされた時
			protected function onChangeData():void
			{
				util.Util.changeData(this.chkDataAscii, this.txtDataArea);
			}
			
			//	[実行] ボタン押下時処理
			protected function onExecute():void
			{
				//	実行結果の情報をクリア
				this.txtIDm.text					= "";
				this.txtPMm.text					= "";
				this.txtStatusFlag1.text			= "";
				this.txtStatusFlag2.text			= "";
				this.txtFeliCaErrorCode.text		= "";
				this.txtRWErrorCode.text			= "";
				this.txtNFCProxyErrorCode.text	= "";
				
				//	リスナーの登録
				this.configureEventListeners(true);
				
				//	NFC Type4 Tag のデータを書き込みする為の情報の設定
				var request:NFCType4TagRequest = new NFCType4TagRequest();
				
				request.type	= NFCType4TagRequest.WRITE;		// NFC Type4 Tag のデータを書き込むときのプロパティ
				
				//	書き込むデータを画面から取得する。
				//	書き込むデータは16進文字列で指定します。
				//	最大数は 16バイト * 13ブロックです。				
				var i:uint;
				var temp:String;
				if (this.chkDataAscii.selected  == true)
				{
					var data:ByteArray = new ByteArray();
					data.writeUTFBytes(this.txtDataArea.text);
					
					request.data = util.Util.bytesToString(data);			
				}
				else
				{
					request.data = this.txtDataArea.text;
					request.data = request.data.replace(new RegExp("/\r\n|\r/*", "g"), "");
				}				
				
				//	ログの表示
				this.displayMessage("NFC Type4 Tag のデータ書き込みを実行します。");
				this.displayMessage("  data = ");
				var msg:String;
				for (i=0; i < request.data.length; i +=(16*2))
				{
					msg = request.data.substring(i, (i+(16*2)));
					this.displayMessage("         " + msg);
				}
				
				//	NFC Type4 Tag のデータを読み込みを実行
				FeliCaSample(parentDocument.parentDocument).fc.access(request);
			}
			
			//	FeliCa アクセス完了時
			protected function onFeliCaAccessComplete(evt:FeliCaStatusEvent):void
			{
				var response:NFCType4TagResponse = evt.object as NFCType4TagResponse;
				
				//	エラーコードの表示
				this.txtFeliCaErrorCode.text		= response.felicaError.toString();		// FeliCaライブラリでエラーが発生した場合に felica_error がセットされます。正常時は 0
				this.txtRWErrorCode.text			= response.rwError.toString();			// FeliCaライブラリでエラーが発生した場合に rw_error がセットされます。正常時は 0
				this.txtNFCProxyErrorCode.text	= response.felicaProxyError.toString();	// NFCProxy内部でエラーが発生した場合に felica_proxy_error がセットされます。 正常時は 0
				
				//	ログの表示
				this.displayMessage("NFC Type4 Tag のデータ書き込みに成功しました。");
				this.displayMessage("  felica error = " + response.felicaError.toString());
				this.displayMessage("  rw error = " + response.rwError.toString());
				this.displayMessage("  proxy error = " + response.felicaProxyError.toString());
				
				//	リスナーの削除
				this.configureEventListeners(false);
			}
			
			//	FeliCa アクセス実行失敗時
			protected function onFeliCaAccessFailure(evt:FeliCaStatusEvent):void
			{
				if (evt.object is FeliCaAccessError)
				{
					var accessErr:FeliCaAccessError = evt.object as FeliCaAccessError;					
			
					//	エラーコードの表示
					this.txtFeliCaErrorCode.text		= accessErr.felicaError.toString();		// FeliCaライブラリでエラーが発生した場合に felica_error がセットされます。正常時は 0
					this.txtRWErrorCode.text			= accessErr.rwError.toString();			// FeliCaライブラリでエラーが発生した場合に rw_error がセットされます。正常時は 0
					this.txtNFCProxyErrorCode.text	= accessErr.felicaProxyError.toString();	// NFCProxy内部でエラーが発生した場合に felica_proxy_error がセットされます。 正常時は 0
					
					//	ログの表示
					this.displayMessage("NFC Type4 Tag のデータ書き込みに失敗しました。");
					this.displayMessage("  ERROR : (" + accessErr.errorID + ") " + accessErr.message);
					this.displayMessage("  felica error = " + accessErr.felicaError.toString());
					this.displayMessage("  rw error     = " + accessErr.rwError.toString());
					this.displayMessage("  proxy error  = " + accessErr.felicaProxyError.toString());
				}
				else if (evt.object is Error)
				{
					var error:Error = evt.object as Error;
					
					//	ログの表示
					this.displayMessage("NFC Type4 Tag のデータ書き込みに失敗しました。");
					this.displayMessage("  ERROR : (" + error.errorID + ") " + error.message);
				}
				
				//	リスナー削除
				this.configureEventListeners(false);
			}
			
			//	FeliCa アクセス実行時のパラメータエラー発生時
			protected function onFeliCaAccessParameterError(evt:FeliCaStatusEvent):void
			{
				var error:Error = evt.object as Error;
				
				//	ログの表示
				this.displayMessage("NFC Type4 Tag のデータ書き込みでエラーが発生しました。");
				this.displayMessage("  ERROR : (" + error.errorID + ") " + error.message);
				
				//	リスナー削除
				this.configureEventListeners(false);
			}
			
			//	FeliCa アクセスで使用するイベントリスナーの登録・削除
			private function configureEventListeners(addFlag:Boolean):void
			{
				if (addFlag == true)
				{
					//	リスナー登録
					FeliCaSample(parentDocument.parentDocument).fc.addEventListener(FeliCaStatusEvent.FELICA_ACCESS_COMPLETE, onFeliCaAccessComplete);
					FeliCaSample(parentDocument.parentDocument).fc.addEventListener(FeliCaStatusEvent.FELICA_ACCESS_FAILURE, onFeliCaAccessFailure);
					FeliCaSample(parentDocument.parentDocument).fc.addEventListener(FeliCaStatusEvent.FELICA_ACCESS_PARAMETER_ERROR, onFeliCaAccessParameterError);
				}
				else
				{
					//	リスナー削除
					FeliCaSample(parentDocument.parentDocument).fc.removeEventListener(FeliCaStatusEvent.FELICA_ACCESS_COMPLETE, onFeliCaAccessComplete);
					FeliCaSample(parentDocument.parentDocument).fc.removeEventListener(FeliCaStatusEvent.FELICA_ACCESS_FAILURE, onFeliCaAccessFailure);
					FeliCaSample(parentDocument.parentDocument).fc.removeEventListener(FeliCaStatusEvent.FELICA_ACCESS_PARAMETER_ERROR, onFeliCaAccessParameterError);
				}
				
				//	画面の使用可・不可の設定
				FeliCaSample(parentDocument.parentDocument).enabled = (addFlag == true) ? false : true;
			}
			
			//	ログの表示
			private function displayMessage(msg:String):void
			{
				this.txtLogArea.text += msg;
				this.txtLogArea.text += "\n";
				
				this.callLater(setScroll);	//	一番下にスクロールさせる
			}
			
			//	ログエリアを一番下にスクロールさせる
			private function setScroll():void
			{
				this.txtLogArea.verticalScrollPosition = this.txtLogArea.maxVerticalScrollPosition;
			}
		]]>
	</mx:Script>	
</mx:Canvas>
